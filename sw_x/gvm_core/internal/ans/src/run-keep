#! /bin/csh -fb

onintr leave

set log  = /tmp/ans_keep_test.log.$$
set iter = 0

while (1)
    make ans_keep_test

    valgrind --leak-check=full ./ans_keep_test |& tee $log || break;

    grep "==[0-9]*== ERROR SUMMARY: 0 errors from" $log > /dev/null

    if ($status == 1) then
        echo "Got an error from valgrind"
        break;
    endif

    grep "==[0-9]*== LEAK SUMMARY" $log > /dev/null

    if ($status == 0) then
        echo "valgrind found a memory leak"
        break;
    endif

    sleep 2
    @ iter++;
    echo " === Finished iteration $iter"
end

echo "The log is $log"

exit 1

leave:
    rm -f $log
    echo "Interrupted"
